TITLE Temperature Error Corrector    (Proj6_landryza.asm)

; Author: Zachary Landry
; Last Modified: 06/04/25
; OSU email address: landryza@oregonstate.edu
; Course number/section:   CS271 Section 400
; Project Number: 6               Due Date: 06/08/25
; Description: This program takes in a file containing an string of temperatures 
;				and prints the temperatures in reverse order.     

INCLUDE Irvine32.inc

  ;--------------------------------------------------------------------------
  ; Name: mGetString
  ;
  ;  Calls mDisplayString, then reads an input string from the user
  ;
  ; preconditions: strOffset and userInput are byte arrays
  ;
  ; receives: 
  ;				strOffset  = adress of array
  ;				userInput  = empty array
  ;				maxCount   = max length of input string
  ;
  ; returns: a string in userInput
  ;--------------------------------------------------------------------------
mGetString		MACRO	strOffset, userInput, maxCount
	PUSH	EAX
	PUSH	ECX
	PUSH	EDX
	mDisplayString OFFSET strOffset
	MOV		EDX, OFFSET userInput		; saves input to variable
	MOV		ECX, maxCount				; sets maximum input size
	CALL	ReadString
	POP		EDX
	POP		ECX
	POP		EAX	
ENDM

  ;--------------------------------------------------------------------------
  ; Name: mDisplayString
  ;
  ;  Prints a string out to the user
  ;
  ; receives: 
  ;				strOffset  = address of array
  ;--------------------------------------------------------------------------
mDisplayString	MACRO	strOffset
	PUSH	EDX
	MOV		EDX, strOffset
	CALL	WriteString
	POP		EDX
ENDM

  ;--------------------------------------------------------------------------
  ; Name: mDisplayChar
  ;
  ;  Prints a character out to the user
  ;
  ; receives: 
  ;				character  = ASCII code for character
  ;--------------------------------------------------------------------------
mDisplayChar	MACRO	character
	PUSH	EAX
	MOV		AL, character
	CALL	WriteChar
	POP		EAX
ENDM


DELIMITER		=	44
TEMPS_PER_DAY	=	24
FILE_NAME_SIZE	=	20
MAX_FILE_SIZE	=	200

.data

intro1			BYTE		"Welcome to the temperature error corrector! I'll read a '",0
intro2			BYTE		"'-delimited file storing a series of temperature values.",13,10,0
intro3			BYTE		"The file must be ASCII-formatted. I'll then reverse the ordering and provide the corrected temperature ordering as a printout!",13,10,13,10,0
prompt1			BYTE		"Enter the name of the file to be read: ",0
correctedLabel	BYTE		13,10,13,10,"Here's the corrected temperature order!",13,10,0
tempArray		SDWORD		TEMPS_PER_DAY DUP(?)			; to be filled after reading file
fileName		BYTE		FILE_NAME_SIZE DUP(?)			; user input
fileHandle      DWORD		?								; generated by OpenInputFile
fileBuffer		BYTE		MAX_FILE_SIZE DUP(?)
bytesRead		DWORD		?								; to count the length of the file read
outro1			BYTE		13,10,13,10,"Hope that helps resolve the issue, goodbye!",13,10,0
fileNameError   BYTE		"Invalid Filename... Please ensure the file is in the same directory as the ASM file.",13,10,0
fileReadError   BYTE		"Error in Reading File.",13,10,0

.code
main PROC

	;Introduce Program
	mDisplayString	OFFSET intro1
	mDisplayChar	DELIMITER
	mDisplayString	OFFSET intro2
	mDisplayString	OFFSET intro3

	; Get file name from user
	mGetString		OFFSET prompt1, fileName, FILE_NAME_SIZE

	; Attempt to Open Input File
	MOV		EDX, OFFSET fileName
	CALL	OpenInputFile
	MOV		fileHandle, EAX

	; Verify correct filename/file path
	CMP		EAX, INVALID_HANDLE_VALUE
	JNE		_validName    ; Filename Checks Out!
	mDisplayString	OFFSET fileNameError
	JMP		_failEndFile       ; Quit

	;  Attempt to Read Input File to Memory
_validName:
	MOV		EAX, fileHandle
	MOV		EDX, OFFSET fileBuffer 
	MOV		ECX, MAX_FILE_SIZE - 1
	CALL	ReadFromFile
	MOV		bytesRead, EAX
	JNC		_endFile    ;Jump if No Error in Reading File
	mDisplayString	OFFSET fileReadError
	JMP		_failEndFile       ; Quit

	;  Closes out of the Input File
_endFile:
	MOV		EAX, fileHandle
	CALL	CloseFile

	;  Moves the provided temperatures into an array
	PUSH	bytesRead
	PUSH	OFFSET fileBuffer
	PUSH	OFFSET tempArray
	CALL	ParseTempsFromString

	;  Prints out the label for corrected temperatures
	mDisplayString	OFFSET correctedLabel

	;  Prints the provided temperatures in reverse order
	PUSH	OFFSET tempArray
	CALL	WriteTempsReverse

	;  Farewell
	mDisplayString OFFSET outro1
	JMP		_endProcedure

	;  Closes file and ends procedure if file read fails
_failEndFile:
	MOV		EAX, fileHandle
	CALL	CloseFile

_endProcedure:
	Invoke ExitProcess,0	; exit to operating system
main ENDP

  ;--------------------------------------------------------------------------
  ; Name: ParseTempsFromString
  ;
  ; Procedure to move the temperatures read from the file into an array
  ;
  ; preconditions: fileBuffer contains only numbers and delimiters
  ;
  ; postconditions: none
  ;
  ; receives: 
  ;				bytesRead		   =  a DWORD
  ;				OFFSET fileBuffer  =  address of an array of BYTEs
  ;				OFFSET tempArray   =  address of an array of SDWORDs
  ;
  ;			[EBP+16] = bytesRead
  ;			[EBP+12] = OFFSET fileBuffer
  ;			[EBP+8] = OFFSET tempArray
  ;			[EBP+4] = return address
  ;			[EBP] = old ebp
  ;
  ; returns: tempArray filled with integers
  ;--------------------------------------------------------------------------
ParseTempsFromString	PROC
	LOCAL	numInt:SDWORD		; calculation placeholder
	LOCAL	isNegative:DWORD	; used as a sign flag
	PUSH	ESI
	PUSH	EDI
	PUSH	ECX
	PUSH	EAX
	PUSH	EBX

	;  Initialize registers and variables
	MOV		ESI, [EBP+12]
	MOV		EDI, [EBP+8]
	MOV		ECX, 1
	MOV		numInt, 0
	MOV		isNegative, 0

	;  Checks to see if byte is number, delimiter, or negative character
_readByte:
	LODSB
	CMP		EAX, DELIMITER
	JE		_checkNegative
	CMP		EAX, 45
	JNE		_number
	MOV		isNegative, 1
	JMP		_nextByte

	;  Converts a number from ASCII to decimal
_number:
	SUB		EAX, 48
	PUSH	EAX
	MOV		EAX, numInt
	MOV		EBX, 10
	MUL		EBX
	MOV		EBX, EAX
	POP		EAX
	ADD		EAX, EBX
	MOV		numInt, EAX
	JMP		_nextByte

	;  Checks to see if number was negative
_checkNegative:
	MOV		EAX, numInt
	CMP		isNegative, 1
	JNE		_storeNumber
	NEG		EAX

	;  Saves number to tempArray
_storeNumber:
	STOSD
	XOR		EAX, EAX
	MOV		numInt, EAX 
	MOV		isNegative, EAX

	;  increment ecx, check if ecx <= bytesRead, if so loop again
_nextByte:
	INC		ECX
	CMP		ECX, [EBP+16]
	JBE		_readByte

	POP		EBX
	POP		EAX
	POP		ECX
	POP		EDI
	POP		ESI
	RET		12
ParseTempsFromString	ENDP


  ;--------------------------------------------------------------------------
  ; Name: WriteTempsReverse
  ;
  ; Prints an array to the console in reverse order. Calls mDisplayChar to 
  ;		insert a character between values
  ;
  ; preconditions: the array contains SDWORDs
  ;
  ; receives: 
  ;				OFFSET tempArray  =  address of array
  ;
  ;		[EBP+8] = OFFSET tempArray
  ;		[EBP+4] = return address
  ;		[EBP] = old ebp
  ;
  ; returns: prints out array to the console
  ;--------------------------------------------------------------------------
WriteTempsReverse		PROC
	PUSH	EBP
	MOV		EBP, ESP
	PUSH	ESI
	PUSH	EAX

	;  Initialize registers and set counter
	MOV		ESI, [EBP+8]
	ADD		ESI, TEMPS_PER_DAY*4		; Moves to end of array
	SUB		ESI, 4
	MOV		ECX, 1

	;  Loop to print temperature and DELIMITER
_writeTemp:
	STD
	LODSD
	CALL			WriteInt
	mDisplayChar	DELIMITER

	;  increment ecx, check if ecx <= total number of temps, if so loop again
	INC		ECX
	CMP		ECX, TEMPS_PER_DAY
	JBE		_writeTemp

	POP		EAX
	POP		ESI
	POP		EBP
	RET		4
WriteTempsReverse		ENDP

END main
